plugins {
	id 'java'
	id 'application'
	id 'eclipse'
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'etri'
version = '0.9.0'
mainClassName = 'mdt.cli.MDTCommandsMain'

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}
[compileJava,compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
	mavenCentral()
}

ext {
	logback_version = '1.3.14'
	guava_version = '33.1.0-jre'
	lombok_version = '1.18.32'
	
	picocli_version = '4.7.6'
	text_tree_version = '2.1.2'
	text_table_version = '1.2.4'

	okhttp_version = '4.12.0'
	paho_version = '1.2.5'
	commons_beanutils_version = '1.9.4'
	
	aas4j_version = '1.0.2'
}

dependencies {
	implementation project(":utils")

	implementation "com.google.guava:guava:${guava_version}"
	implementation "ch.qos.logback:logback-classic:${logback_version}"
	compileOnly "org.projectlombok:lombok:${lombok_version}"
	annotationProcessor "org.projectlombok:lombok:${lombok_version}"
	
	// command line parser
	implementation "info.picocli:picocli:${picocli_version}"
	
	implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: "${okhttp_version}"
	
	implementation "org.nocrala.tools.texttablefmt:text-table-formatter:${text_table_version}"
	implementation "org.barfuin.texttree:text-tree:${text_tree_version}"
	
	// AAS model
	implementation "org.eclipse.digitaltwin.aas4j:aas4j-dataformat-json:${aas4j_version}"
	implementation "org.eclipse.digitaltwin.aas4j:aas4j-dataformat-aasx:${aas4j_version}"
	
	// Apache Commons BeanUtils
	implementation "commons-beanutils:commons-beanutils:${commons_beanutils_version}"
	
	// MQTT
//	implementation "org.eclipse.paho:org.eclipse.paho.client.mqttv3:${paho_version}"
}

task sourceJar(type: Jar) {
	from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
	from javadoc.destinationDir
}

shadowJar {
	mergeServiceFiles()
	
	exclude 'META-INF/*.SF'
	exclude 'META-INF/*.DSA'
	exclude 'META-INF/*.RSA'
}

eclipse {
	classpath {
		file {
			withXml {
				def node = it.asNode()
				node.appendNode('classpathentry', [kind: 'output', path: 'build/classes'])
			}
			
			whenMerged { cp ->
				Map entryByPath = cp.entries.groupBy { entry -> entry.path }
				entryByPath.each { key, values ->
					if ( values.size() > 1 ) {
            			def entry = values.first()
            			if ( entry.kind == 'src' ) {
            				entry.includes = []
            				entry.excludes = []
            			}
            			int index = cp.entries.indexOf entry
            			cp.entries.removeAll values
            			cp.entries.add index, entry
					}
				}
				cp.entries.each { entry ->
					if ( entry.kind == 'src' && entry.hasProperty('output') ) {
						if ( entry.path.contains('/test/') ) {
							entry.output = 'build/test_classes'
						}
						else {
							entry.output = 'build/classes'
						}
					}
				}
			}
		}
	}
}